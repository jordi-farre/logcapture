buildscript {
  repositories {
    mavenLocal()
    jcenter()
    maven { url 'https://dl.bintray.com/jspcore/maven' }
  }

  dependencies {
  }
}

plugins {
  id "com.jfrog.bintray" version "1.8.4"
}

group 'com.jspcore'
version '0.4.7-SNAPSHOT'
description = 'A testing library for assert logging messages.'

apply plugin: 'idea'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'java-library'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
  jcenter()
}

ext {
  junit5_version = "5.2.0"
}

dependencies {
  api "org.hamcrest:hamcrest-library:2.1"
  implementation "org.slf4j:slf4j-api:1.7.22"
  implementation "ch.qos.logback:logback-classic:1.1.8"
  implementation "org.awaitility:awaitility:3.1.6"
  implementation "junit:junit:4.12"
  implementation "org.junit.jupiter:junit-jupiter-api:$junit5_version"
  implementation 'org.spockframework:spock-core:1.3-groovy-2.5'

  testImplementation "org.assertj:assertj-core:3.6.1"
  testImplementation "org.junit.jupiter:junit-jupiter-engine:$junit5_version"
  testImplementation "org.junit.jupiter:junit-jupiter-params:$junit5_version"
  testImplementation "org.junit.vintage:junit-vintage-engine:$junit5_version"
}

test {
  useJUnitPlatform {
    includeEngines 'junit-jupiter', 'junit-vintage'
  }
  testLogging {
    events "passed", "skipped", "failed"
  }
}

apply plugin: "jacoco"
jacocoTestReport {
  reports {
    xml.enabled true
    html.enabled true
  }
}

def pomConfig = {
  name project.name
  description project.description
  url "http://www.jspcore.com/"

  licenses {
    license {
      name "MIT License"
      url "https://opensource.org/licenses/MIT"
      distribution "repo"
    }
  }
  scm {
    connection "scm:https://mustaine@github.com/mustaine/logcapture.git"
    developerConnection "scm:git@github.com:mustaine/logcapture.git"
    url "https://github.com/mustaine/logcapture"
  }
  developers {
    developer {
      id "jspcore"
      name "Team JSPCore"
    }
  }
}

task javadocJar(type: Jar) {
  classifier = 'javadoc'
  from javadoc
}

task sourceJar(type: Jar) {
  from sourceSets.main.allJava
}

wrapper {
  gradleVersion = '5.4'
}

publishing {
  publications {
    MyPublication(MavenPublication) {
      from components.java
      groupId 'com.jspcore'
      artifactId 'logcapture'
      version '0.4.7'

      artifacts {
        artifact sourceJar {
          classifier "sources"
        }

        artifact javadocJar {
          classifier "javadoc"
        }
      }

      pom.withXml {
        def root = asNode()
        asNode().appendNode('description', project.description)
        root.children().last() + pomConfig
      }
    }
  }
}

bintray {
//  dryRun = true
  user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
  key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

  publications = ['MyPublication']
  pkg {
    repo = 'maven'
    name = 'logcapture'
    userOrg = 'jspcore'
    licenses = ['MIT']
    vcsUrl = 'https://github.com/mustaine/logcapture.git'
    version {
      name = '0.4.7'
      vcsTag = '0.4.7'
      released = new Date()
    }
  }
}



